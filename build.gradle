import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'maven-publish'
    id 'fabric-loom' version "0.4-SNAPSHOT"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = "SwiftPublicRepo"
        url = uri("https://raw.githubusercontent.com/SwiftHQ/SwiftPublicRepo/master")
    }
    maven {
        name = 'mojang'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'legacy-fabric'
        url = 'https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven'
    }
    maven {
        url = 'https://dl.bintray.com/boogiemonster1o1/cool-mods/'
    }
}

minecraft {
    intermediaryUrl = {
        return "https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven/net/fabricmc/intermediary/" + it + "/intermediary-" + it + "-v2.jar";
    }
}

configurations {
    shade
}

dependencies {
    compile "com.google.guava:guava:23.5-jre"
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modCompile ("net.fabricmc:fabric-loader-1.8.9:${project.loader_version}") {
        exclude module: "guava"
    }
    implementation 'com.mojang:brigadier:1.0.17'
    shade 'com.mojang:brigadier:1.0.17'
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.8.2"
    modImplementation "io.github.legacy-fabric-community:shadowedconfig:1.1.0"
}

tasks.remapJar.dependsOn(shadowJar)
(tasks.remapJar.input as FileSystemLocationProperty<? extends FileSystemLocation>).set(shadowJar.archivePath)

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:minecraft:.*'))
        exclude(dependency('net.fabricmc:.*:.*'))
        exclude(dependency('io.github.legacy-fabric-community:.*:.*'))
    }
    configurations = [project.configurations.shade]
    relocate 'com.mojang.brigadier', 'io.github.hydos.holotext.shadowed.com.mojang.brigadier'
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}